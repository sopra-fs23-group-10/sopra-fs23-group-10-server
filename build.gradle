plugins {
    id "io.freefair.lombok" version "6.5.0-rc1"
    id 'org.springframework.boot' version '2.4.13'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'idea'
    id 'jacoco'
    id "org.sonarqube" version "3.5.0.2730"
}

group 'ch.uzh.ifi.hasel'
version '1.0.0'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

springBoot {
    mainClass.set('ch.uzh.ifi.hase.soprafs23.Application')
}

dependencies {

    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    implementation 'org.mapstruct:mapstruct:1.3.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'

    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'

    implementation 'org.springframework:spring-context:5.3.14'

    implementation 'com.mailjet:mailjet-client:5.2.2'

    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'org.json:json:20210307'

    implementation 'org.postgresql:postgresql:42.3.1'
    implementation 'com.google.cloud.sql:postgres-socket-factory:1.11.1'
}

bootJar {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

sonarqube {
    properties {
        property "sonar.projectKey", "sopra-fs23-group-10_sopra-fs23-group-10-server"
        property "sonar.organization", "sopra-fs23-group-10"
        property "sonar.host.url", "https://sonarcloud.io"

        property 'sonar.java.coveragePlugin', 'jacoco'
        property 'sonar.coverage.jacoco.xmlReportPaths', file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml").path
        property 'sonar.test.inclusions', '**/*Test.class'
        property 'sonar.exclusions', '**/generated/**'
        property 'sonar.jacoco.reportPaths', file("${project.buildDir}/jacoco/test.exec").path
        property 'sonar.jacoco.reportMissing.force.zero', 'true'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    maxParallelForks = 1
}

File secretPropsFile = file('./local.properties')
if (secretPropsFile.exists()) {
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
}

defaultTasks 'bootJar', 'build'